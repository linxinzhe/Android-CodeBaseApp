apply plugin: 'com.android.application'
apply plugin: 'im.fir.plugin.gradle'
apply plugin: 'realm-android'
apply plugin: 'com.neenbedankt.android-apt'

def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 100 + Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.linxinzhe.android.codebaseapp"
        minSdkVersion 15
        targetSdkVersion 24
//        versionCode gitCommitCount
//        versionName gitTag
        versionCode 1
        versionName "0.5"
//        multiDexEnabled true
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    signingConfigs {
        debug {
            storeFile file("demo_debug.jks")
            storePassword 'Py6mtRWL4AqeyJ'
            keyAlias 'demo'
            keyPassword 'Py6mtRWL4AqeyJ'
        }
        release {
            storeFile file("demo_release.jks")
            storePassword project.hasProperty('STOREPASS') ? STOREPASS : ''
            keyAlias project.hasProperty('KEYALIAS') ? KEYALIAS : ''
            keyPassword project.hasProperty('KEYPASS') ? KEYPASS : ''
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            useProguard true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            manifestPlaceholders = [XXXMAP_API_KEY: "ds4ubnOVP9Y7B45l8GQEddXK"]
            signingConfig signingConfigs.debug
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    // 输出apk名称为CodeBase_v1.0_2015-01-15_wandoujia.apk
                    def fileName = "CodeBase_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
//        exclude 'proguard-project.txt'
        exclude 'project.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        umeng {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng", XXXMAP_API_KEY: "xxx"]
        }
    }
}

fir {
    apiToken 'xx'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //    compile 'com.android.support:multidex:1.0.1'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'

    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okio:okio:1.9.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    compile 'com.orhanobut:logger:1.15'

    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'in.srain.cube:ultra-ptr:1.0.11'
    compile 'in.srain.cube:cube-sdk:1.0.44@aar'

    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.tencent.bugly:crashreport:latest.release'

    compile 'me.dm7.barcodescanner:zxing:1.9'

    compile 'cn.finalteam:galleryfinal:1.4.8.7'

    compile 'io.realm:android-adapters:1.3.0'
}
